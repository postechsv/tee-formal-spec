
--- This file defines the syntax of imperative language and its semantics.

fmod IMP-TYPE is
  sort Type .
endfm

view Type from TRIV to IMP-TYPE is
  sort Elt to Type .
endv

fmod IMP-BTYPE is
  pr IMP-TYPE .

  sort BType .
  subsort BType < Type .

  op int : -> BType [ctor] .
  op float : -> BType [ctor] .
  op bool : -> BType [ctor] .
  op void : -> BType [ctor] .
  op auto : -> BType [ctor] .
endfm

fmod IMP-STYPE is
  pr IMP-TYPE .

  sort SType .
  subsort SType < Type .

  op struct_ : Type -> SType [ctor] .
endfm

view SType from TRIV to IMP-STYPE is
  sort Elt to SType .
endv

fmod IMP-REF-TYPE is
  pr IMP-TYPE .

  sort RType .
  subsort RType < Type .
--- T is a type => T* is a type.
--- T is a type => T& is a type.
  op _* : Type -> RType [ctor] .
  op _& : Type -> RType [ctor] .
endfm

--- This module defines label for goto statement.

fmod IMP-LABEL is
  sorts Label .
endfm

view Label from TRIV to IMP-LABEL is
  sort Elt to Label .
endv

--- Values in IMP. Namely, Bool and Int.

fmod IMP-VALUE is
  pr INT .
  sorts LValue Value Value? .
  subsort LValue < Value < Value? .

  op undef : -> Value? [ctor] .
  op #_ : Bool -> Value [ctor] .
  op #_ : Int -> Value [ctor] .
endfm

view Value? from TRIV to IMP-VALUE is
  sort Elt to Value? .
endv

--- Variable identifiers in IMP.

fmod IMP-VAR is
  sort Var .
endfm

view Var from TRIV to IMP-VAR is
  sort Elt to Var .
endv

--- Base expressions in IMP

fmod IMP-BASE-EXP is
  pr IMP-VALUE .
  pr IMP-VAR .

--- PExp : Var Decl.
--- RExp : RValue
--- FExp : Function related
--- LExp : LValue
  sorts PExp RExp FExp LExp Exp .
  subsort Value < Exp .
  subsort LValue Var < LExp < Exp .
  subsort PExp RExp FExp < Exp .
endfm

fmod IMP-LIST is
  sort ImpList .
  op nil : -> ImpList [ctor] .
  op _,_ : ImpList ImpList -> ImpList [ctor assoc id: nil prec 70] .
endfm

--- Lists of expressions, variable ids, and values.

fmod IMP-EXP-LIST is
  inc IMP-BASE-EXP .
  inc IMP-LIST .

  sort ExpList .
  subsort Exp ImpList < ExpList .
  op _,_ : ExpList ExpList -> ExpList [ctor ditto] .

  sort VarList .
  subsorts Var ImpList < VarList < ExpList .
  op _,_ : VarList VarList -> VarList [ctor ditto] .

  sort ValueList .
  subsorts Value ImpList < ValueList < ExpList .
  op _,_ : ValueList ValueList -> ValueList [ctor ditto] .
endfm

--- Function Signature

fmod IMP-FUNC-SIG is
  pr IMP-TYPE .
  pr IMP-VAR .
  pr IMP-EXP-LIST .

  sorts FuncSig FuncId .

  var T : Type .
  var F : FuncId .
  var E : Exp .
  var EL : ExpList .

--- FuncSig

  op __(_) : Type FuncId ExpList -> FuncSig [ctor] .
  op __() : Type FuncId -> FuncSig [ctor] .

  eq T F() = T F(nil) .

--- For later assertion, not necessary
  op  checkPExp : ExpList -> Bool .
  ceq checkPExp((E, EL)) = checkPExp(EL) if E :: PExp .
   eq checkPExp(EL) = false [owise] .
endfm

view FuncSig from TRIV to IMP-FUNC-SIG is
  sort Elt to FuncSig .
endv

--- LValues for IMP

fmod IMP-LVALUE is
  pr IMP-BASE-EXP .
  pr IMP-TYPE .
  pr MAP{Var,Nat} * (op _,_ to _;_) .

  op sobj : Map{Var,Nat} -> Value [ctor] .
  op lvalue : Exp -> Exp [ctor] .
  op loc : Nat -> Value [ctor] .
  op ref : Nat -> Value [ctor] .
endfm

--- Expressions in IMP

fmod IMP-EXP is
  pr IMP-FUNC-SIG .
  inc IMP-EXP-LIST .

  var F : FuncId .
  vars E E1 E2 : Exp .
  var LE : LExp .

  op _+_ : Exp Exp -> Exp [ctor gather(E e) prec 33] .
  op _-_ : Exp Exp -> Exp [ctor gather(E e) prec 33] .
  op _*_ : Exp Exp -> Exp [ctor gather(E e) prec 31] .
  op _/_ : Exp Exp -> Exp [ctor gather(E e) prec 31] .
  op _<=_ : Exp Exp -> Exp [ctor gather(E e) prec 37] .
  op _==_ : Exp Exp -> Exp [ctor gather(E e) prec 37] .
  op _&&_ : Exp Exp -> Exp [ctor gather(E e) prec 55] .
  op _||_ : Exp Exp -> Exp [ctor gather(E e) prec 55] .
  op !_ : Exp -> Exp [ctor prec 53] .
  op _._ : LExp Var -> LExp [ctor gather(E e) prec 30] .
  op __ : Type Var -> PExp [ctor prec 30] .

  op _(_) : FuncId ExpList -> FExp [ctor prec 30] .
  op _() : FuncId -> FExp [ctor prec 30] .

  eq F () = F (nil) .

--- Aux
  --- op read : LExp -> RExp .
  --- op eval : Exp -> Exp .

  --- eq eval(E1 + E2) = eval(E1) + eval(E2) .
  --- eq eval(E1 - E2) = eval(E1) - eval(E2) .
  --- eq eval(E1 * E2) = eval(E1) * eval(E2) .
  --- eq eval(E1 / E2) = eval(E1) / eval(E2) .
  --- eq eval(E1 <= E2) = eval(E1) <= eval(E2) .
  --- eq eval(E1 == E2) = eval(E1) == eval(E2) .
  --- eq eval(E1 && E2) = eval(E1) && eval(E2) .
  --- eq eval(E1 || E2) = eval(E1) || eval(E2) .
  --- eq eval(! E) = ! eval(E) .
  --- eq eval(LE) = read(LE) .
  --- eq eval(E) = E [owise] .

endfm

--- Statements in IMP

fmod IMP-STMT is
  pr TEE-API .
  pr TEE-CLIENT-API .
  pr IMP-EXP .
  pr IMP-LABEL .
  pr IMP-TYPE .
  pr IMP-STYPE .
  pr IMP-BTYPE .
  pr IMP-REF-TYPE .

  sort Stmt .

  var F : FuncId .
  var T : Type .
  var X : Var .
  var S S' : Stmt .
  var E : Exp .
  var EL : ExpList .

  op _; : Exp -> Stmt [ctor prec 60] .
  op __ : Stmt Stmt -> Stmt [ctor assoc prec 62] .

  op skip : -> Stmt [ctor] .
  op _:=_; : Exp Exp -> Stmt [ctor prec 60] .
  op _::=_; : Exp Exp -> Stmt [ctor prec 60] .
  op if(_){_}else{_} : Exp Stmt Stmt -> Stmt [ctor prec 60] .
  op if(_){_} : Exp Stmt -> Stmt [ctor prec 60] .

  eq if(E){S} = if(E){S}else{skip} .

  op while(_){_} : Exp Stmt -> Stmt [ctor prec 60] .
  op goto_; : Label -> Stmt [ctor prec 60] .
  op _:_ : Label Stmt -> Stmt [ctor prec 60] .

--- Structure & function declaration

  op _{_}; : SType Stmt -> Stmt [ctor prec 49] .
  op _{_}  : FuncSig Stmt -> Stmt [ctor prec 49] .

--- Function call

  op return_; : Exp -> Stmt [ctor prec 60] .


--- decl & initialization

  eq T X ::= E ; = T X ; X := E ; .

------------------------------------------------------------
--- Internal KLabel
------------------------------------------------------------

  op $push : -> Stmt [ctor] .
  op $bind(_|_|_) : ExpList ExpList ExpList -> Stmt [ctor] .
  op $exec : Stmt -> Stmt [ctor] .
  op $setLoc : Var Nat -> Stmt [ctor] .
  op $waitSysCall : TeeApi -> Stmt [ctor] .
  op $assign : ExpList ExpList -> Stmt [ctor] .
  op $prep(_) : ExpList -> Stmt [ctor] .
  op $in(_) : ValueList -> Stmt [ctor] .
  op $out(_) : ExpList -> Stmt [ctor] .
  op $out(_|_) : ExpList ExpList -> Stmt [ctor] .
  op $alloc : Type -> Stmt [ctor] .
  op $setLoc : Var -> Stmt [ctor] .
  op $updateLoc : Var Value -> Stmt [ctor] .
  op $member : Var -> Stmt [ctor] .
  op $update : Var Var -> Stmt [ctor] .
  op $eval : Exp Exp -> Stmt [ctor] .

------------------------------------------------------------
--- Auxiliary Functions
------------------------------------------------------------

  sort SElem SList .
  subsort SElem < SList .

  op nilElem : -> SList [ctor] .
  op <_> : Stmt -> SElem .
  op __ : SList SList -> SList [assoc id: nilElem] .
  
  op getSList : Stmt -> SList .
  eq getSList(S S') = < S > getSList(S') .
  eq getSList(S) = < S > [owise] .
endfm

--- view Stmt from TRIV to IMP-STMT is
---   sort Elt to Stmt .
--- endv

view Stmt from TRIV to IMP-STMT is
  sort Elt to Stmt .
endv

--- IMP programs

fmod IMP-PROGRAM is
  pr IMP-STMT .

  sort Program .
  subsort Stmt < Program .
endfm

--- Evaluation contexts

fmod IMP-EVAL-CONTEXT is
  pr IMP-PROGRAM .
  pr IMP-LVALUE .
  inc K-COMPUTATION .

  ops `[`]+_ `[`]-_  `[`]*_ `[`]/_ `[`]<=_ `[`]&&_ `[`]||_ `[`]==_ : Exp -> KLabel [ctor] .
  ops _+`[`] _-`[`] _*`[`] _/`[`] _<=`[`] _&&`[`] _||`[`] _==`[`] : Value -> KLabel [ctor] .
  op !`[`] : -> KLabel [ctor] .
  ops `[`]._ _.`[`] : Exp -> KLabel [ctor] .
  op $prep(`[`],_) : ExpList -> KLabel [ctor] .

  --- op lvalue : Exp -> KLabel [ctor] .
  op lvalue(`[`]) : -> KLabel [ctor] .

  op read(`[`]) : -> KLabel [ctor] .

  --- op eval : Exp -> KLabel [ctor] .
  op $eval(`[`],_) : Exp -> KLabel [ctor] .

  op `[`]:=_; : Value -> KLabel [ctor] .
  op _:=`[`]; : Exp -> KLabel [ctor] .
  op `[`]; : -> KLabel [ctor prec 68] .

  op if(`[`]){_}else{_} : Stmt Stmt -> KLabel [ctor] .

  op return`[`]; : -> KLabel [ctor] .

------------------------------------------------------------
--- Internal KLabel
------------------------------------------------------------

  op $decl(_|_) : SList Map{Var,Nat} -> KLabel [ctor] .
  op $loc : Nat -> KLabel [ctor] .
  op $setStruct : Map{Var,Nat} -> KLabel [ctor] .

endfm

--- Heating/cooling rules for IMP

fmod IMP-STRICTNESS is
  pr IMP-EVAL-CONTEXT .
  inc K-CONFIG .

  subsort Exp Program < KLabel .

  var T : Type .
  vars E E1 E2 : Exp .
  vars V V1 V2 : Value .
  vars S S1 S2 : Stmt .
  var X : Var .
  var K : K .
  var F : FuncId .
  var EL : ExpList .
  var VL : ValueList .
  var STRT : Map{Var,Nat} .

  ceq k(E ; ~> K) = k(E ~> []; ~> K) if not E :: Value .
   eq k(V ~> []; ~> K) = k(K) .
   eq k([]; ~> K)      = k(K) .

  ceq k(E1 + E2 ~> K)       = k(E1 ~> [] + E2 ~> K) if not E1 :: Value .
   eq k(V1 ~> [] + E2 ~> K) = k(V1 + E2 ~> K) .

  ceq k(V1 + E2 ~> K)       = k(E2 ~> V1 + [] ~> K) if not E2 :: Value .
   eq k(V2 ~> V1 + [] ~> K) = k(V1 + V2 ~> K) .

  ceq k(E1 - E2 ~> K)       = k(E1 ~> [] - E2 ~> K) if not E1 :: Value .
   eq k(V1 ~> [] - E2 ~> K) = k(V1 - E2 ~> K) .

  ceq k(V1 - E2 ~> K)       = k(E2 ~> V1 - [] ~> K) if not E2 :: Value .
   eq k(V2 ~> V1 - [] ~> K) = k(V1 - V2 ~> K) .

  ceq k(E1 * E2 ~> K)       = k(E1 ~> [] * E2 ~> K) if not E1 :: Value .
   eq k(V1 ~> [] * E2 ~> K) = k(V1 * E2 ~> K) .

  ceq k(V1 * E2 ~> K)       = k(E2 ~> V1 * [] ~> K) if not E2 :: Value .
   eq k(V2 ~> V1 * [] ~> K) = k(V1 * V2 ~> K) .

  ceq k(E1 / E2 ~> K)       = k(E1 ~> [] / E2 ~> K) if not E1 :: Value .
   eq k(V1 ~> [] / E2 ~> K) = k(V1 / E2 ~> K) .

  ceq k(V1 / E2 ~> K)       = k(E2 ~> V1 / [] ~> K) if not E2 :: Value .
   eq k(V2 ~> V1 / [] ~> K) = k(V1 / V2 ~> K) .

  ceq k(E1 <= E2 ~> K)       = k(E1 ~> [] <= E2 ~> K) if not E1 :: Value .
   eq k(V1 ~> [] <= E2 ~> K) = k(V1 <= E2 ~> K) .

  ceq k(V1 <= E2 ~> K)       = k(E2 ~> V1 <= [] ~> K) if not E2 :: Value .
   eq k(V2 ~> V1 <= [] ~> K) = k(V1 <= V2 ~> K) .

  ceq k(E1 == E2 ~> K)       = k(E1 ~> [] == E2 ~> K) if not E1 :: Value .
   eq k(V1 ~> [] == E2 ~> K) = k(V1 == E2 ~> K) .

  ceq k(V1 == E2 ~> K)       = k(E2 ~> V1 == [] ~> K) if not E2 :: Value .
   eq k(V2 ~> V1 == [] ~> K) = k(V1 == V2 ~> K) .

  ceq k(E1 && E2 ~> K)       = k(E1 ~> [] && E2 ~> K) if not E1 :: Value .
   eq k(V1 ~> [] && E2 ~> K) = k(V1 && E2 ~> K) .

  ceq k(V1 && E2 ~> K)       = k(E2 ~> V1 && [] ~> K) if not E2 :: Value .
   eq k(V2 ~> V1 && [] ~> K) = k(V1 && V2 ~> K) .

  ceq k(E1 || E2 ~> K)       = k(E1 ~> [] || E2 ~> K) if not E1 :: Value .
   eq k(V1 ~> [] || E2 ~> K) = k(V1 || E2 ~> K) .

  ceq k(V1 || E2 ~> K)       = k(E2 ~> V1 || [] ~> K) if not E2 :: Value .
   eq k(V2 ~> V1 || [] ~> K) = k(V1 || V2 ~> K) .

  ceq k(! E ~> K)       = k(E ~> ! [] ~> K) if not E :: Value .
   eq k(V ~> ! [] ~> K) = k(! V ~> K) .

--- assign

  ceq k(E1 := E2 ; ~> K)      = k(E2 ~> E1 := []; ~> K) if not (E2 :: Value or E1 :: PExp) .
   eq k(V2 ~> E1 := []; ~> K) = k(E1 := V2 ; ~> K) .

  ceq k(E1 := V2 ; ~> K)       = k(lvalue(E1) ~> [] := V2 ; ~> K) if not (E1 :: Value or E1 :: PExp) .
   eq k(V1 ~> [] := V2 ; ~> K) = k(V1 := V2 ; ~> K) .

--- struct member access

  ceq k(E1 . E2 ~> K)        = k(lvalue(E1) ~> [] . E2 ~> K) if not E1 :: Value .
   eq k(V1 ~> [] . E2 ~> K)  = k(V1 . E2 ~> K) .

  ceq k(lvalue(E1 . E2) ~> K)        = k(E1 . E2 ~> lvalue([]) ~> K) if not E1 :: Value .
   eq k(V1 . E2 ~> lvalue([]) ~> K)  = k(lvalue(V1 . E2) ~> K) .

--- Conditional statement

  ceq k(if (E) {S1} else {S2} ~> K)       
    = k(E ~> if ([]) {S1} else {S2} ~> K) if not E :: Value .
  ceq k(V ~> if ([]) {S1} else {S2} ~> K) 
    = k(if (V) {S1} else {S2} ~> K) if not V :: LValue .

  --- ceq k(return E ; ~> K) = k(E ~> return []; ~> K) if not E :: Value .
  ceq k(return E ; ~> K) = k(E ~> return []; ~> K) if not E :: Value .
   eq k(V ~> return []; ~> K) = k(return V ; ~> K) .

--- Call-by-value & call-by-reference

  ceq k($eval(E, T X) ~> K) = if T :: RType then k(lvalue(E) ~> $eval([], T X) ~> K)
                              else k(E ~> $eval([], T X) ~> K) fi if not E :: Value .

   eq k(V ~> $eval([], T X) ~> K) = k($eval(V, T X) ~> K) .

--- TODO
  ceq k($prep(E , EL) ~> K) = k(E ~> $prep([], EL) ~> K) if not E :: Value .
   eq k(V ~> $prep([], EL) ~> $in(VL) ~> K) = k($prep(EL) ~> $in(VL, V) ~> K) .

endfm

--- Call stacks for IMP

fmod IMP-CALL-STACK is
  pr MAP{Var,Nat} * (op _,_ to _;_) .
  pr MAP{Label,Stmt} * (op undefined to nolabel) .
  inc K-COMPUTATION .

  sort StackItem .
  op item : Map{Var,Nat} Map{Label,Stmt} K Nat -> StackItem [ctor] .

  sort CallStack .
  op nilStack : -> CallStack [ctor] .
  op _::_ : StackItem CallStack -> CallStack [ctor] .
endfm

--- K configurations for IMP

fmod IMP-CONFIG is
  pr IMP-PROGRAM .
  pr IMP-CALL-STACK .
  pr MAP{Label,Stmt} * (op undefined to nolabel) .
  pr MAP{Nat,Value?} * (op _,_ to _;_) .
  pr MAP{SType,Stmt} * (op undefined to noSDecl) .
  pr MAP{FuncSig,Stmt} * (op undefined to noFDecl) .
  inc K-CONFIG .

  op env : Map{Var,Nat} -> KConfigItem [ctor] .
  op store : Map{Nat,Value?} -> KConfigItem [ctor] .
  op sdecl : Map{SType,Stmt} -> KConfigItem [ctor] .
  op fdecl : Map{FuncSig,Stmt} -> KConfigItem [ctor] .
  op fstack : CallStack -> KConfigItem [ctor] .
  op nextLoc : Nat -> KConfigItem [ctor] .
  op labels : Map{Label,Stmt} -> KConfigItem [ctor] .

endfm

mod IMP-AUX is
  pr IMP-CONFIG .
  pr IMP-STRICTNESS .

  vars S S' : Stmt .
  var LB : Label .
  var LM : Map{Label,Stmt} .
  var P : Program .
  var N : Nat .
  vars X Y : Var .
  var T : Type .
------------------------------------------------------------
--- Auxiliary Functions
------------------------------------------------------------

  op mkInitConf : Program -> KConfig .
  op getLabels : Program Map{Label,Stmt} -> Map{Label,Stmt} .

  eq mkInitConf(P) = k(P) env(empty) store(empty) fdecl(empty) sdecl(empty) fstack(nilStack) nextLoc(0) labels(getLabels(P, empty)) .

  eq getLabels(LB : S , LM) = insert(LB , S , getLabels(S , LM)) .
  eq getLabels(S S' , LM) = getLabels(S , LM) , getLabels(S', LM) .
  eq getLabels(S , LM) = empty [owise] .

  op getOffSet : Stmt Var -> Nat .
  eq getOffSet(S, X) = getOffSet(S, X, 1) .

  op getOffSet : Stmt Var Nat -> Nat .
  eq getOffSet(T X ; S, X, N) = N .
  eq getOffSet(T X ;, X, N) = N .
  eq getOffSet(T Y ; S, X, N) = getOffSet(S, X, N + 1) [owise] .

  op isBaseType : Type -> Bool .
  eq isBaseType(struct T) = false .
  eq isBaseType(T) = true .
endm

--- IMP Semantics

mod IMP-SEMANTICS is
  pr IMP-AUX .

  var N : Nat .
  vars T T' : Type .
  var ST : SType .
  vars X X' Y : Var .
  var XL : ExpList .
  var PARAMS : ExpList .
  vars K K' : K .
  vars V V' : Value? .
  vars ENV ENV' : Map{Var,Nat} .
  vars SO : Map{Var,Nat} .
  var MEM : Map{Nat,Value?} .
  var B : Bool .
  vars L L' I I' : Int .
  vars E E' E1 E2 : Exp .
  vars S S' : Stmt .
  var SL : SList .
  var F : FuncId .
  var TEEAPI : TeeApi .
  var SDCL : Map{SType,Stmt} .
  var FDCL : Map{FuncSig,Stmt} .
  var STIT : StackItem .
  var STACK : CallStack .
  var VL : ValueList .
  vars EL EL' : ExpList .
  var LB : Label .
  vars LM LM' : Map{Label,Stmt} .
  var P : Program .
  var FSIG : FuncSig .

------------------------------------------------------------
--- IMP Semantics
------------------------------------------------------------

  rl k(X ~> K) env((X |-> L) ; ENV) store((L |-> V) ; MEM)
  => k(V ~> K) env((X |-> L) ; ENV) store((L |-> V) ; MEM) .

  rl k(lvalue(X) ~> K) env((X |-> L) ; ENV)
  => k(loc(L) ~> K) env((X |-> L) ; ENV) .

  rl [struct-member-access]: k(loc(L) . X ~> K) store((L |-> sobj((X |-> N) ; SO)) ; (N |-> V) ; MEM) 
                          => k(V ~> K) store((L |-> sobj((X |-> N) ; SO)) ; (N |-> V) ; MEM) .
  rl [struct-member-access]: k(lvalue(loc(L) . X) ~> K) store((L |-> sobj((X |-> N) ; SO)) ; MEM) 
                          => k(loc(N) ~> K) store((L |-> sobj((X |-> N) ; SO)) ; MEM) .

--- Assign

  rl k(loc(L) := V ; ~> K) store(MEM)
  => k(                 K) store(insert(L, V, MEM)) .

--- exp

  rl  k(# I + # I' ~> K)   => k(# (I + I') ~> K) .
  rl  k(# I - # I' ~> K)   => k(# (I - I') ~> K) .
  rl  k(# I * # I' ~> K)   => k(# (I * I') ~> K) .
  crl k(# I / # I' ~> K)   => k(# (I quo I') ~> K) if I' =/= 0 .
  rl  k(# I <= # I' ~> K)  => k(# (I <= I') ~> K) .
  rl  k(V == V' ~> K)     => k(# (V == V') ~> K) .
  rl  k(# true  && E ~> K) => k(E ~> K) .
  rl  k(# false && E ~> K) => k(# false ~> K) .
  rl  k(# true  || E ~> K) => k(# true ~> K) .
  rl  k(# false || E ~> K) => k(E ~> K) .
  rl  k(! (# B) ~> K)      => k(# (not B) ~> K) .

  rl k(skip ~> K) 
  => k(K) .

  rl k(K ~> S S' ~> K') => k(K ~> S ~> S' ~> K') .

  eq k(goto LB ; ~> K) labels(((LB |-> S) , LM)) = k(S) labels(((LB |-> S) , LM)) .
  eq k(goto LB ; ~> K) labels(LM) = k(K) labels(LM) [owise] .
  eq k(LB : S) = k(S) .

  rl k(if (# true)  {S} else {S'} ~> K) => k(S  ~> K) .
  rl k(if (# false) {S} else {S'} ~> K) => k(S' ~> K) .

  rl k(while (E) {S} ~> K) 
  => k(if (E) {(S while (E) {S})} else {skip} ~> K) .

--- declaration
--- for simplicity we don't allow nested structure.

  rl [assn]:     k(T X ~> K) => k($alloc(T) ~> $setLoc(X) ~> K) .

  rl [sAlloc-i]: k($alloc(struct T) ~> K) sdecl((struct T |-> S , SDCL))
              => k($decl(getSList(S) | empty) ~> $alloc(T) ~> K) sdecl((struct T |-> S , SDCL)) .

  rl [sAlloc-e]: k($decl(nilElem | SO) ~> $alloc(T) ~> K) => k($alloc(T) ~> $setStruct(SO) ~> K) .
  rl [setStruc]: k($loc(L) ~> $setStruct(SO) ~> K) store((L |-> V) ; MEM)
              => k($loc(L) ~> K) store((L |-> sobj(SO)) ; MEM) .

  rl [sDecl]:    k($decl(< T Y ; > SL | SO) ~> K) => k($alloc(T) ~> $member(Y) ~> $decl(SL | SO) ~> K) .
  rl [sMemDecl]: k($loc(L) ~> $member(Y) ~> $decl(SL | SO) ~> K) => k($decl(SL | ((Y |-> L) ; SO)) ~> K) .

--- alloc

  crl [alloc]:   k($alloc(T) ~> K) store(MEM) nextLoc(L) => k($loc(L) ~> K) store(insert(L, undef, MEM)) nextLoc(L + 1) if isBaseType(T) .
  rl [decl]:     k($loc(L) ~> $setLoc(X) ~> K) env(ENV) => k(K) env(insert(X, L, ENV)) .

--- function & structure declaration

  rl k(FSIG {S} ~> K) fdecl(FDCL) => k(K) fdecl(insert(FSIG, S, FDCL)) .
  rl k(struct T {S} ; ~> K) sdecl(SDCL) => k(K) sdecl(insert(struct T, S, SDCL)) .

--- function call

  rl k(F(EL) ~> K) fdecl(((T F(PARAMS) |-> S) , FDCL))
  => k($push ~> $bind(EL | PARAMS | nil) ~> $exec(S) ~> K) fdecl(((T F(PARAMS) |-> S) , FDCL)) .

  rl k($push ~> $bind(EL | PARAMS | nil) ~> $exec(S) ~> K) env(ENV) fstack(STACK) nextLoc(L) labels(LM)
  => k($bind(EL | PARAMS | nil) ~> $exec(S)) env(ENV) fstack(item(ENV,LM,K,L) :: STACK) nextLoc(L) labels(LM) . 

--- call by reference & call by value

  rl [fBind]: k($bind(nil | T X, XL | V, VL) ~> K) => 
              if T :: RType then k(T X ; ~> $updateLoc(X, V) ~> $bind(nil | XL | VL) ~> K) 
              else k(T X ; X := V ; ~> $bind(nil | XL | VL) ~> K) fi .

  rl [fBind]: k($bind(nil | nil | nil) ~> $exec(S)) => k(S) .

  rl [eval-i]: k($bind(E, EL | T X, XL | VL) ~> K) => k($eval(E, T X) ~> $bind(EL | XL, T X | VL) ~> K) .
  rl [eval-e]: k($eval(V, T X) ~> $bind(EL | XL | VL) ~> K) => k($bind(EL | XL | VL, V) ~> K) . 

  rl [updateLoc]: k($updateLoc(X, loc(L)) ~> K) env((X |-> L') ; ENV) => k(K) env((X |-> L) ; ENV) .

--- Return statement

  rl k(return V ; ~> K) env(ENV) fstack(item(ENV',LM',K',L') :: STACK) nextLoc(L) labels(LM)
  => k(V ~> K') env(ENV') fstack(STACK) nextLoc(L') labels(LM') .

  rl k(.K) env(ENV) fstack(item(ENV',LM',K',L') :: STACK) nextLoc(L) labels(LM)
  => k(K') env(ENV') fstack(STACK) nextLoc(L') labels(LM') . --- for void return

--- TODO: Clean

  rl k($prep(nil) ~> K) => k(K) .

  rl k($assign((E,EL), (E',EL')) ~> K) => k(E' := E ; ~> $assign(EL, EL') ~> K) .
  rl k($assign(EL, nil) ~> K) => k(K) .
endm

--- This module provides invisible reduction for IMP semantics.

mod IMP-SEMANTICS-INVISIBLE is
  pr IMP-AUX .

  var N : Nat .
  vars T T' : Type .
  var ST : SType .
  vars X X' Y : Var .
  var XL : ExpList .
  var PARAMS : ExpList .
  vars K K' : K .
  vars V V' : Value? .
  vars ENV ENV' : Map{Var,Nat} .
  vars SO : Map{Var,Nat} .
  var MEM : Map{Nat,Value?} .
  var B : Bool .
  vars L L' I I' : Int .
  vars E E' E1 E2 : Exp .
  vars S S' : Stmt .
  var SL : SList .
  var F : FuncId .
  var TEEAPI : TeeApi .
  var SDCL : Map{SType,Stmt} .
  var FDCL : Map{FuncSig,Stmt} .
  var STIT : StackItem .
  var STACK : CallStack .
  var VL : ValueList .
  vars EL EL' : ExpList .
  var LB : Label .
  vars LM LM' : Map{Label,Stmt} .
  var P : Program .
  var FSIG : FuncSig .

------------------------------------------------------------
--- IMP Semantics
------------------------------------------------------------

  eq k(X ~> K) env((X |-> L) ; ENV) store((L |-> V) ; MEM)
   = k(V ~> K) env((X |-> L) ; ENV) store((L |-> V) ; MEM) .

  eq k(lvalue(X) ~> K) env((X |-> L) ; ENV)
   = k(loc(L) ~> K) env((X |-> L) ; ENV) .

  eq [struct-member-access]: k(loc(L) . X ~> K) store((L |-> sobj((X |-> N) ; SO)) ; (N |-> V) ; MEM) 
                           = k(V ~> K) store((L |-> sobj((X |-> N) ; SO)) ; (N |-> V) ; MEM) .
  eq [struct-member-access]: k(lvalue(loc(L) . X) ~> K) store((L |-> sobj((X |-> N) ; SO)) ; MEM) 
                           = k(loc(N) ~> K) store((L |-> sobj((X |-> N) ; SO)) ; MEM) .

--- Assign

  eq k(loc(L) := V ; ~> K) store(MEM)
   = k(                 K) store(insert(L, V, MEM)) .

--- exp

  eq  k(# I + # I' ~> K)   = k(# (I + I') ~> K) .
  eq  k(# I - # I' ~> K)   = k(# (I - I') ~> K) .
  eq  k(# I * # I' ~> K)   = k(# (I * I') ~> K) .
  ceq k(# I / # I' ~> K)   = k(# (I quo I') ~> K) if I' =/= 0 .
  eq  k(# I <= # I' ~> K)  = k(# (I <= I') ~> K) .
  eq  k(V == V' ~> K)      = k(# (V == V') ~> K) .
  eq  k(# true  && E ~> K) = k(E ~> K) .
  eq  k(# false && E ~> K) = k(# false ~> K) .
  eq  k(# true  || E ~> K) = k(# true ~> K) .
  eq  k(# false || E ~> K) = k(E ~> K) .
  eq  k(! (# B) ~> K)      = k(# (not B) ~> K) .

  eq k(skip ~> K) 
   = k(K) .

  eq k(K ~> S S' ~> K') = k(K ~> S ~> S' ~> K') .

  eq k(goto LB ; ~> K) labels(((LB |-> S) , LM)) = k(S) labels(((LB |-> S) , LM)) .
  eq k(goto LB ; ~> K) labels(LM) = k(K) labels(LM) [owise] .
  eq k(LB : S) = k(S) .

  eq k(if (# true)  {S} else {S'} ~> K) = k(S  ~> K) .
  eq k(if (# false) {S} else {S'} ~> K) = k(S' ~> K) .

  eq k(while (E) {S} ~> K) 
  = k(if (E) {(S while (E) {S})} else {skip} ~> K) .

--- declaration
--- for simplicity we don't allow nested structure.

  eq k(T X ~> K) = k($alloc(T) ~> $setLoc(X) ~> K) .

  eq k($alloc(struct T) ~> K) sdecl((struct T |-> S , SDCL))
   = k($decl(getSList(S) | empty) ~> $alloc(T) ~> K) sdecl((struct T |-> S , SDCL)) .

  eq k($decl(nilElem | SO) ~> $alloc(T) ~> K) = k($alloc(T) ~> $setStruct(SO) ~> K) .
  eq k($loc(L) ~> $setStruct(SO) ~> K) store((L |-> V) ; MEM)
   = k($loc(L) ~> K) store((L |-> sobj(SO)) ; MEM) .

  eq k($decl(< T Y ; > SL | SO) ~> K) = k($alloc(T) ~> $member(Y) ~> $decl(SL | SO) ~> K) .
  eq k($loc(L) ~> $member(Y) ~> $decl(SL | SO) ~> K) = k($decl(SL | ((Y |-> L) ; SO)) ~> K) .

--- alloc

  ceq k($alloc(T) ~> K) store(MEM) nextLoc(L) = k($loc(L) ~> K) store(insert(L, undef, MEM)) nextLoc(L + 1) if isBaseType(T) .
  eq k($loc(L) ~> $setLoc(X) ~> K) env(ENV) = k(K) env(insert(X, L, ENV)) .

--- function & structure declaration

  eq k(FSIG {S} ~> K) fdecl(FDCL) = k(K) fdecl(insert(FSIG, S, FDCL)) .
  eq k(struct T {S} ; ~> K) sdecl(SDCL) = k(K) sdecl(insert(struct T, S, SDCL)) .

--- function call

  eq k(F(EL) ~> K) fdecl(((T F(PARAMS) |-> S) , FDCL))
   = k($push ~> $bind(EL | PARAMS | nil) ~> $exec(S) ~> K) fdecl(((T F(PARAMS) |-> S) , FDCL)) .

  eq k($push ~> $bind(EL | PARAMS | nil) ~> $exec(S) ~> K) env(ENV) fstack(STACK) nextLoc(L) labels(LM)
   = k($bind(EL | PARAMS | nil) ~> $exec(S)) env(ENV) fstack(item(ENV,LM,K,L) :: STACK) nextLoc(L) labels(LM) . 

--- call by reference & call by value

  eq k($bind(nil | T X, XL | V, VL) ~> K) = if T :: RType then k(T X ; ~> $updateLoc(X, V) ~> $bind(nil | XL | VL) ~> K) 
                                            else k(T X ; X := V ; ~> $bind(nil | XL | VL) ~> K) fi .

  eq k($bind(nil | nil | nil) ~> $exec(S)) = k(S) .

  eq k($bind(E, EL | T X, XL | VL) ~> K) = k($eval(E, T X) ~> $bind(EL | XL, T X | VL) ~> K) .
  eq k($eval(V, T X) ~> $bind(EL | XL | VL) ~> K) = k($bind(EL | XL | VL, V) ~> K) . 

  eq k($updateLoc(X, loc(L)) ~> K) env((X |-> L') ; ENV) = k(K) env((X |-> L) ; ENV) .

--- Return statement

  eq k(return V ; ~> K) env(ENV) fstack(item(ENV',LM',K',L') :: STACK) nextLoc(L) labels(LM)
   = k(V ~> K') env(ENV') fstack(STACK) nextLoc(L') labels(LM') .

  eq k(.K) env(ENV) fstack(item(ENV',LM',K',L') :: STACK) nextLoc(L) labels(LM)
   = k(K') env(ENV') fstack(STACK) nextLoc(L') labels(LM') . --- for void return

--- TODO: Clean

  eq k($assign((E,EL), (E',EL')) ~> K) = k(E' := E ; ~> $assign(EL, EL') ~> K) .
  eq k($assign(EL, nil) ~> K) = k(K) .

endm